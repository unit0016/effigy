/datum/dna
	///	This variable is read by the regenerate_organs() proc to know what organ subtype to give
	var/wing_type = NO_VARIATION

/datum/species/regenerate_organs(mob/living/carbon/target, datum/species/old_species, replace_current = TRUE, list/excluded_zones, visual_only = FALSE, replace_missing = TRUE)
	. = ..()
	if(target == null)
		return
	if(!ishuman(target))
		return

	if(target.dna.wing_type != NO_VARIATION && is_type_in_typecache(src, GLOB.bodypart_allowed_species[FEATURE_WINGS]))
		if(target.dna.wing_type == "Moth Wings" && target.dna.features["moth_wings"] != /datum/sprite_accessory/blank::name)
			var/obj/item/organ/replacement = SSwardrobe.provide_type(/obj/item/organ/wings/moth)
			replacement.Insert(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
			return .
		else if(target.dna.wing_type == "Wings" && target.dna.features["wings_anthro"] != /datum/sprite_accessory/blank::name)
			var/obj/item/organ/replacement = SSwardrobe.provide_type(/obj/item/organ/wings/more)
			replacement.Insert(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
			return .

	var/obj/item/organ/wings/old_part = target.get_organ_slot(ORGAN_SLOT_EXTERNAL_WINGS)
	if(istype(old_part))
		old_part.Remove(target, special = TRUE, movement_flags = DELETE_IF_REPLACED)
		old_part.moveToNullspace()

/// Wings variation
/datum/preference/choiced/wing_variation
	savefile_key = "wing_type"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_SECONDARY_FEATURES
	priority = PREFERENCE_PRIORITY_DEFAULT

/datum/preference/choiced/wing_variation/apply_to_human(mob/living/carbon/human/target, chosen_variation)
	target.dna.wing_type = chosen_variation
	switch(chosen_variation)
		if(NO_VARIATION)
			target.dna.features["wings_anthro"] = /datum/sprite_accessory/blank::name
			target.dna.features["moth_wings"] = /datum/sprite_accessory/blank::name
		if("Wings")
			target.dna.features["moth_wings"] = /datum/sprite_accessory/blank::name
		if("Moth Wings")
			target.dna.features["wings_anthro"] = /datum/sprite_accessory/blank::name

/datum/preference/choiced/wing_variation/create_default_value()
	return NO_VARIATION

/datum/preference/choiced/wing_variation/init_possible_values()
	return list(NO_VARIATION, "Wings", "Moth Wings")

/datum/preference/choiced/wing_variation/is_accessible(datum/preferences/preferences)
	. = ..()
	var/datum/species/species = preferences.read_preference(/datum/preference/choiced/species)
	if(!is_type_in_typecache(species, GLOB.bodypart_allowed_species[FEATURE_WINGS]))
		return FALSE

	return TRUE

///	Wings type
/datum/preference/choiced/species_feature/moth_wings/icon_for(value)
	var/datum/sprite_accessory/moth_wings = get_accessory_for_value(value)
	return generate_back_icon(moth_wings, "moth_wings")

/datum/preference/choiced/species_feature/wings
	savefile_key = "feature_wings"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_CLOTHING
	should_generate_icons = TRUE
	main_feature_name = "Wings"
	feature_key = "wings_anthro"

/datum/preference/choiced/species_feature/wings/compile_constant_data()
	var/list/data = ..()
	data[SUPPLEMENTAL_FEATURE_KEY] = /datum/preference/tri_color/wings_color::savefile_key
	return data

/datum/preference/choiced/species_feature/wings/create_default_value()
	return /datum/sprite_accessory/blank::name

/datum/preference/choiced/species_feature/wings/icon_for(value)
	var/datum/sprite_accessory/wings = get_accessory_for_value(value)
	return generate_back_icon(wings, "wings")

/datum/preference/choiced/species_feature/wings/is_accessible(datum/preferences/preferences)
	. = ..()
	var/datum/species/species = preferences.read_preference(/datum/preference/choiced/species)
	if(!is_type_in_typecache(species, GLOB.bodypart_allowed_species[FEATURE_WINGS]))
		return FALSE

	var/chosen_variation = preferences.read_preference(/datum/preference/choiced/wing_variation)
	if(chosen_variation == "Wings")
		return TRUE

	return FALSE

///	Moth wings type
/datum/preference/choiced/species_feature/moth_wings
	category = PREFERENCE_CATEGORY_CLOTHING

/datum/preference/choiced/species_feature/moth_wings/compile_constant_data()
	var/list/data = ..()
	data[SUPPLEMENTAL_FEATURE_KEY] = /datum/preference/tri_color/wings_color::savefile_key
	return data

/datum/preference/choiced/species_feature/moth_wings/create_default_value()
	return /datum/sprite_accessory/blank::name

/datum/preference/choiced/species_feature/moth_wings/is_accessible(datum/preferences/preferences)
	. = ..()
	var/datum/species/species = preferences.read_preference(/datum/preference/choiced/species)
	if(!is_type_in_typecache(species, GLOB.bodypart_allowed_species[FEATURE_WINGS]))
		return FALSE

	var/chosen_variation = preferences.read_preference(/datum/preference/choiced/wing_variation)
	if(chosen_variation == "Moth Wings")
		return TRUE

	return FALSE

/datum/bodypart_overlay/mutant/wings/more
	layers = EXTERNAL_FRONT | EXTERNAL_FRONT_2 | EXTERNAL_FRONT_3 | EXTERNAL_ADJACENT | EXTERNAL_ADJACENT_2 | EXTERNAL_ADJACENT_3 | EXTERNAL_BEHIND | EXTERNAL_BEHIND_2 | EXTERNAL_BEHIND_3
	feature_key = "wings_anthro"
	feature_key_sprite = "wings"

/datum/bodypart_overlay/mutant/wings/more/color_image(image/overlay, draw_layer, obj/item/bodypart/limb)
	if(limb == null)
		return ..()
	if(limb.owner == null)
		return ..()
	if(draw_layer == bitflag_to_layer(EXTERNAL_FRONT))
		overlay.color = limb.owner.dna.features["wings_color_1"]
		return overlay
	else if(draw_layer == bitflag_to_layer(EXTERNAL_ADJACENT))
		overlay.color = limb.owner.dna.features["wings_color_1"]
		return overlay
	else if(draw_layer == bitflag_to_layer(EXTERNAL_BEHIND))
		overlay.color = limb.owner.dna.features["wings_color_1"]
		return overlay
	else if(draw_layer == bitflag_to_layer(EXTERNAL_FRONT_2))
		overlay.color = limb.owner.dna.features["wings_color_2"]
		return overlay
	else if(draw_layer == bitflag_to_layer(EXTERNAL_ADJACENT_2))
		overlay.color = limb.owner.dna.features["wings_color_2"]
		return overlay
	else if(draw_layer == bitflag_to_layer(EXTERNAL_BEHIND_2))
		overlay.color = limb.owner.dna.features["wings_color_2"]
		return overlay
	else if(draw_layer == bitflag_to_layer(EXTERNAL_FRONT_3))
		overlay.color = limb.owner.dna.features["wings_color_3"]
		return overlay
	else if(draw_layer == bitflag_to_layer(EXTERNAL_ADJACENT_3))
		overlay.color = limb.owner.dna.features["wings_color_3"]
		return overlay
	else if(draw_layer == bitflag_to_layer(EXTERNAL_BEHIND_3))
		overlay.color = limb.owner.dna.features["wings_color_3"]
		return overlay
	return ..()

//	Moth wings have their own bespoke RGB code.
/datum/bodypart_overlay/mutant/wings/moth/color_image(image/overlay, draw_layer, obj/item/bodypart/limb)
	if(limb == null)
		return ..()
	if(limb.owner == null)
		return ..()
	var/color_intended = COLOR_WHITE

	var/tcol_1 = limb.owner.dna.features["wings_color_1"]
	var/tcol_2 = limb.owner.dna.features["wings_color_2"]
	var/tcol_3 = limb.owner.dna.features["wings_color_3"]
	if(tcol_1 && tcol_2 && tcol_3)
		//this is beyond ugly but it works
		var/r1 = hex2num(copytext(tcol_1, 2, 4)) / 255.0
		var/g1 = hex2num(copytext(tcol_1, 4, 6)) / 255.0
		var/b1 = hex2num(copytext(tcol_1, 6, 8)) / 255.0
		var/r2 = hex2num(copytext(tcol_2, 2, 4)) / 255.0
		var/g2 = hex2num(copytext(tcol_2, 4, 6)) / 255.0
		var/b2 = hex2num(copytext(tcol_2, 6, 8)) / 255.0
		var/r3 = hex2num(copytext(tcol_3, 2, 4)) / 255.0
		var/g3 = hex2num(copytext(tcol_3, 4, 6)) / 255.0
		var/b3 = hex2num(copytext(tcol_3, 6, 8)) / 255.0
		color_intended = list(r1,g1,b1, r2,g2,b2, r3,g3,b3)
	overlay.color = color_intended
	return overlay

/// Wing colors
/datum/preference/tri_color/wings_color
	priority = PREFERENCE_PRIORITY_BODY_TYPE
	savefile_key = "wings_color"
	savefile_identifier = PREFERENCE_CHARACTER
	category = PREFERENCE_CATEGORY_SUPPLEMENTAL_FEATURES

/datum/preference/tri_color/wings_color/apply_to_human(mob/living/carbon/human/target, value)
	target.dna.features["wings_color_1"] = value[1]
	target.dna.features["wings_color_2"] = value[2]
	target.dna.features["wings_color_3"] = value[3]

/datum/preference/tri_color/wings_color/create_default_value()
	return list(sanitize_hexcolor("[pick("7F", "FF")][pick("7F", "FF")][pick("7F", "FF")]"),
	sanitize_hexcolor("[pick("7F", "FF")][pick("7F", "FF")][pick("7F", "FF")]"),
	sanitize_hexcolor("[pick("7F", "FF")][pick("7F", "FF")][pick("7F", "FF")]"))

/datum/preference/tri_color/wings_color/is_valid(value)
	if (!..(value))
		return FALSE
	return TRUE
